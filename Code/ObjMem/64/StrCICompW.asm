; ==================================================================================================
; Title:      StrCICompW.asm
; Author:     G. Friedrich
; Version:    C.1.0
; Notes:      Version C.1.0, October 2017
;               - First release.
; ==================================================================================================


% include @Environ(OBJASM_PATH)\\Code\\OA_Setup64.inc
% include &ObjMemPath&ObjMemWin.cop

.code
; ——————————————————————————————————————————————————————————————————————————————————————————————————
; Procedure:  StrCICompW
; Purpose:    Compare 2 WIDE strings without case sensitivity and length limitation.
; Arguments:  Arg1: -> WIDE string 1.
;             Arg2: -> WIDE string 2.
; Return:     If string 1 < string 2, then eax < 0.
;             If string 1 = string 2, then eax = 0.
;             If string 1 > string 2, then eax > 0.

OPTION PROC:NONE
align ALIGN_CODE
StrCICompW proc pStr1W:POINTER, pStr2W:POINTER, dMaxLen:DWORD
  ;rcx -> Str1W, rdx -> Str2W, r8d = dMaxLen
  xor eax, eax
  inc r8d
@@Char:
  dec r8d
  jnz @@Next
  xor eax, eax                                          ;eax = 0 (same string)
  ret                                                   ;Return
@@Next:
  mov ax, [rcx]                                         ;Load char to compare
  test ax, ax                                           ;Test if end of string
  jz @@Eq?                                              ;Compute result
  cmp ax, [rdx]                                         ;Compare with the char of the other string
  jnz @@ICmp                                            ;Chars are not equal, check if for capitals
  add rcx, sizeof(CHRW)                                 ;Goto for next char
  add rdx, sizeof(CHRW)
  jmp @@Char
@@ICmp:
  cmp ax, 'z'                                           ;Check range 'A'..'Z' or 'a'..'z'
  ja @@Eq?
  cmp ax, 'A'
  jb @@Eq?
  cmp ax, 'a'
  jae @@1
  cmp ax, 'Z'
  ja @@Eq?
@@1:
  xor ax, 20h                                           ;Swap lowercase - uppercase
  cmp ax, [rdx]                                         ;Compare again
  jne @@2                                               ;If not equal, compute result
  add rcx, sizeof(CHRW)                                 ;Goto for next char
  add rdx, sizeof(CHRW)
  jmp @@Char
@@2:
  and ax, not 20h                                       ;Make uppercase
@@Eq?:
  movzx ecx, CHRW ptr [rdx]                             ;Get char
  cmp cx, 'a'                                           ;Check range 'a'..'z'
  jb @@Exit
  cmp cx, 'z'
  ja @@Exit
  and cx, not 20h                                       ;Make uppercase
@@Exit:
  sub eax, ecx                                          ;Subtract to see which is smaller
  ret
StrCICompW endp
OPTION PROC:DEFAULT

end
