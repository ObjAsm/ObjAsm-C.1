; ==================================================================================================
; Title:      MoveWindowVisible.asm
; Author:     G. Friedrich
; Version:    C.1.0
; Notes:      Version C.1.0, October 2017
;               - First release.
; ==================================================================================================


% include @Environ(OBJASM_PATH)\\Code\\OA_Setup32.inc
% include &ObjMemPath&ObjMem.cop

CheckPointAndAdjustPosition macro
  push MONITOR_DEFAULTTONULL
  push esi
  push edi
  call MonitorFromPoint
  .if eax == NULL
    push MONITOR_DEFAULTTONULL
    push esi
    push edi
    call MonitorFromPoint
    lea ecx, MI
    mov MI.cbSize, sizeof MONITORINFO
    invoke GetMonitorInfo, eax, ecx
    .if esi > MI.rcWork.bottom
      sub esi, MI.rcWork.bottom
      sub sdYPos, esi
    .elseif esi < MI.rcWork.top
      sub esi, MI.rcWork.top
      sub sdYPos, esi
    .endif

    .if edi > MI.rcWork.right
      sub edi, MI.rcWork.right
      sub sdXPos, edi
    .elseif edi < MI.rcWork.left
      sub edi, MI.rcWork.left
      sub sdXPos, edi
    .endif
  .endif
endm

.code

; ——————————————————————————————————————————————————————————————————————————————————————————————————
; Procedure:  MoveWindowVisible
; Purpose:    On a multimonitor system, moves a window but remains always in the visible region.
; Arguments:  Arg1: HANDLE of the Window to move.
;             Arg2: Target X position in pixel.
;             Arg3: Target Y position in pixel.
; Return:     Nothing.

align ALIGN_CODE
MoveWindowVisible proc uses edi esi hWnd:HWND, sdXPos:SDWORD, sdYPos:SDWORD
  local WndRect:RECT, dWndHeight:DWORD, dWndWidth:DWORD, MI:MONITORINFO

  ;   1————————2
  ;   | Window |
  ;   3————————4

  invoke GetWindowRect, hWnd, addr WndRect
  mov eax, WndRect.right
  sub eax, WndRect.left
  mov dWndWidth, eax

  mov eax, WndRect.bottom
  sub eax, WndRect.top
  mov dWndHeight, eax

  ;Check point 4
  mov edi, sdXPos
  add edi, dWndWidth
  mov esi, sdYPos
  add esi, dWndHeight
  CheckPointAndAdjustPosition

  ;Check point 3
  mov edi, sdXPos
  mov esi, sdYPos
  add esi, dWndHeight
  CheckPointAndAdjustPosition

  ;Check point 2
  mov edi, sdXPos
  add edi, dWndWidth
  mov esi, sdYPos
  CheckPointAndAdjustPosition

  ;Check point 1
  mov edi, sdXPos
  mov esi, sdYPos
  CheckPointAndAdjustPosition

  invoke MoveWindow, hWnd, sdXPos, sdYPos, dWndWidth, dWndHeight, FALSE
  ret
MoveWindowVisible endp

end
