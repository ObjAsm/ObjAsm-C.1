; ==================================================================================================
; Title:      GUID2Str_WXP.inc
; Author:     G. Friedrich
; Version:    C.1.0
; Notes:      Version C.1.0, October 2017
;               - First release.
;               - Bitness and platform independent code.
; ==================================================================================================


; ——————————————————————————————————————————————————————————————————————————————————————————————————
; Procedure:  GUID2StrW
; Purpose:    Convert a GUID to a WIDE string.
; Arguments:  Arg1: -> Destination WIDE string Buffer.
;                   It must hold at least 36 characters plus a ZTC (= 74 BYTEs).
;             Arg2: -> GUID.
; Return:     Nothing.

align ALIGN_CODE
GUID2StrW proc uses xdi xsi pBuffer:POINTER, pGUID:POINTER
  if TARGET_BITNESS eq 32
    mov edi, pBuffer                                    ;xdi -> Buffer
    mov esi, pGUID                                      ;xsi -> GUID
  else
    mov rdi, rcx                                        ;xdi -> Buffer
    mov rsi, rdx                                        ;xsi -> GUID
  endif
  invoke dword2hexW, xdi, DWORD ptr [xsi]               ;Convert DWORD to hex
  mov CHRW ptr [xdi + 16], "-"                          ;Append "-"
  add xdi, 18
  invoke dword2hexW, xdi, DWORD ptr [xsi + 4]           ;Convert next DWORD to hex
  mov eax, DCHRW ptr [xdi + 08]                         ;Rearrange BYTEs
  mov ecx, DCHRW ptr [xdi + 12]
  mov DCHRW ptr [xdi + 00], eax
  mov DCHRW ptr [xdi + 04], ecx
  mov CHRW ptr [xdi + 08], "-"                          ;Insert "-"
  mov eax, DCHRW ptr [xdi + 00]                 
  mov ecx, DCHRW ptr [xdi + 04]
  mov DCHRW ptr [xdi + 10], eax
  mov DCHRW ptr [xdi + 14], ecx
  mov CHRW ptr [xdi + 18], "-"                          ;Append "-"
  add xdi, 20
  invoke dword2hexW, xdi, DWORD ptr [xsi + 8]           ;Convert next DWORD to hex
  mov eax, DCHRW ptr [xdi + 08]                         ;Rearrange BYTEs
  mov ecx, DCHRW ptr [xdi + 12]
  mov DCHRW ptr [xdi + 00], ecx
  mov DCHRW ptr [xdi + 04], eax
  mov CHRW ptr [xdi + 08], "-"                          ;Insert "-"
  mov eax, DCHRW ptr [xdi + 00]                     
  mov ecx, DCHRW ptr [xdi + 04]
  mov DCHRW ptr [xdi + 10], ecx
  mov DCHRW ptr [xdi + 14], eax
  add xdi, 18
  invoke dword2hexW, xdi, DWORD ptr [xsi + 12]          ;Convert next DWORD to hex
  mov eax, DCHRW ptr [xdi + 08]                         ;Rearrange BYTEs
  mov ecx, DCHRW ptr [xdi + 12]
  mov DCHRW ptr [xdi + 00], ecx
  mov DCHRW ptr [xdi + 04], eax
  mov eax, DCHRW ptr [xdi + 00]                         
  mov ecx, DCHRW ptr [xdi + 04]
  mov DCHRW ptr [xdi + 08], ecx
  mov DCHRW ptr [xdi + 12], eax
  ret
GUID2StrW endp

end
